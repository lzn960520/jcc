using System::IO;
#entry sort::Main

module sort begin

/***
 * the class is the Main class
 */
class Main
begin
	private int[0..] a; // the array

	private procedure sort(int l, int r)
	begin
		var int i = l;
		var int j = r;
		var int mid = a[(l + r) / 2];
		repeat
			while a[i] < mid do i++;
			while a[j] > mid do j--;
			if i <= j then
			begin
				var int temp = a[i];
				a[i] = a[j];
				a[j] = temp;
				i++;
				j--;
			end
		until i > j;
		if i < r then
			sort(i, r);
		if l < j then
			sort(l, j);
	end;
	
	static public function main() : int
	begin
		var Main that = new Main;
		var int N = IO::readInt();
		that.a = new int[N];
		for (var int i = 0; i < N; i++) do
			that.a[i] = IO::readInt();
		that.sort(0, N - 1);
		for (var int i = 0; i < N; i++) do
			IO::writeInt(that.a[i]);
		IO::writeln("");
		return 0;
	end;
end;

end;
